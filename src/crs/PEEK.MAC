	.title	peek	Peek at a location in RSTS/E
	.ident	/000004/
;
;+
;
; Index		Peek at a location in RSTS/E
;
; Usage
;
;	peek(location)
;	int		*location;
;
; Description
;
;	Peek to a location in the RSTS/E monitor.
;
;	The job will be aborted with a BPT if not running under
;	RSTS/E.
;
; Bugs
;
;-
;
; Edit history
; 000001 09-Jan-80 MM	Initial construction
; 000002 27-Mar-80 MM	Merged libraries
; 000003 14-Oct-81 MM	Added .psect
; 000004 19-Jan-02 BQT	Changed for I/D space
;

.iif	ndf	rsx	rsx	=	1	;Assume RSX11M
;
xrb	=	442
.peek	=	emt+12

	.psect	c$code,i,ro

peek::
	tst	$$rsts
	bne	10$
	CRASH			;Sorry

10$:	mov	2(sp),@#xrb
.if eq	rsx							;02+
	emt	377		;.priv, rt11 only
.endc								;02-
	.peek
	mov	@#xrb,r0
	rts	pc
	.end
