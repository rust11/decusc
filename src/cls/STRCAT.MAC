	.title	strcat	String Concatenate
	.ident	/000002/
;
;+
;
; Index		Concatenate a string onto another
;
; Usage
;
;	char *
;	strcat(out, in);
;	char		*out;
;	char		*in;
;
; Description
;
;	Append "in" to "out".  Return out.
;
; Bugs
;
;-
;
; Edit history
; 000001 21-Oct-81 MM	Initial edit
; 000002 15-Jan-02 BQT	Changed for I/D space.
;
	.psect	c$code,i,ro

strcat::
	mov	2(sp),r0
	mov	4(sp),r1
10$:
	tstb	(r0)+			; Skip to the end
	bne	10$			; of the output
	dec	r0			; buffer, r0 -> null trailer

20$:
	movb	(r1)+,(r0)+
	bne	20$
	mov	2(sp),r0
	return
	.end
